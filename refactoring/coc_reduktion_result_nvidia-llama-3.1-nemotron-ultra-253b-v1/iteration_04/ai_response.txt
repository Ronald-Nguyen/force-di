File `classes\di_Binding.cls`:
```apex
public abstract class di_Binding implements Comparable {

    public BindingType BindingType {get; private set;}
    @AuraEnabled public String BindingTypeAsString {get { return BindingType.name();} }
    @AuraEnabled public String NamespacePrefix {get;private set;}
    @AuraEnabled public String DeveloperName {get;private set;}
    @AuraEnabled public SObjectType BindingObject {get;private set;}
    @AuraEnabled public Integer BindingSequence {get;private set;}
    @AuraEnabled public Object To {get;private set;}
    @AuraEnabled public Object Data {get;private set;}
    private Boolean IsProvider = false;
    private Object Injected = null;

    public Object getInstance() {
        return getInstance(null, false);
    }

    public Object getInstance(Object params) {
        return getInstance(params, false);
    }

    public Object getInstance(Object params, Boolean newInstance) {
        if (IsProvider) {
            return newInstance(params);
        } else if (newInstance) {
            return newInstance(params);
        } else if (Injected == null) {
            Injected = newInstance(params);
        }
        return Injected;
    }

    public abstract Object newInstance(Object params);

    public Integer compareTo(Object compareTo) {
        di_Binding binding = (di_Binding) compareTo;
        return this.toString().compareTo(binding.toString());
    }

    public override String toString() {
        String hashValue = 
            (BindingObject != null) ? 
            BindingObject.getDescribe().getName() : 
            DeveloperName;
        if(BindingSequence != null) {
            hashValue += '#' + String.valueOf(BindingSequence).leftPad(4,'0');
        }
        return hashValue;
    }

    public interface Provider {
        Object newInstance(Object params);
    }

    public class Resolver {
        private String developerName;
        private SObjectType bindingObject;
        private boolean bindingsAreRequired = true;
        private List<di_Module> modules = null;
        private List<di_Binding> bindings = null;

        public Resolver(List<di_Module> modules) {
            this.modules = modules;
        }

        public Resolver set(di_Module module) {
            modules.clear();
            modules.add(module);
            bindings = null; 
            return this;
        }

        public Resolver add(di_Module module) {
            modules.add(module);
            bindings = null; 
            return this;
        }

        public Resolver byName(String developerName) {
            this.developerName = developerName;
            return this;
        }

        public Resolver bySObject(SObjectType bindingObject) {
            this.bindingObject = bindingObject;
            return this;
        }

        public Resolver emptyBindingsAllowed() {
            bindingsAreRequired = false;
            return this;
        }

        public Resolver replaceBindingWith( Object mockType ) {
            loadBindings();
            for (di_Binding bind : bindings) {
                if (isBindingMatchByFilteringCriteria(bind)) {
                    bind = di_Binding.newInstance(di_Binding.BindingType.Apex, this.developerName, this.bindingObject, null, mockType, null);
                    break;
                }
            }
            this.developerName = null;
            this.bindingObject = null;
            return this;
        }

        private Boolean isBindingMatchByFilteringCriteria(di_Binding bind) {
            Boolean isMatch = false;
            if (String.isNotBlank(this.developerName) && this.bindingObject != null) {
                if ((this.developerName.equalsIgnoreCase(bind.DeveloperName) || 
                     this.developerName.equalsIgnoreCase(di_NamespaceClass.CURRENTNAMESPACE + '.' + bind.DeveloperName)) &&
                    this.bindingObject == bind.BindingObject) {
                    isMatch = true;
                }
            } else {
                if (String.isNotBlank(this.developerName) && 
                    (this.developerName.equalsIgnoreCase(bind.DeveloperName) || 
                     this.developerName.equalsIgnoreCase(di_NamespaceClass.CURRENTNAMESPACE + '.' + bind.DeveloperName))) {
                    isMatch = true;
                } else if (this.bindingObject != null && bind.BindingObject == this.bindingObject) {
                    isMatch = true;
                }
            }
            return isMatch;
        }

        private void loadBindings() {
            if (bindings == null) {
                bindings = new List<di_Binding>();
                for (di_Module module : modules) {
                    module.configure();
                    for (di_Binding bind : module.getBindings()) {
                        if (bind.BindingType == di_Binding.BindingType.Module) {
                            di_Module embeddedModule = (di_Module) bind.getInstance();
                            embeddedModule.configure();
                            bindings.addAll(embeddedModule.getBindings());
                        } else {
                            bindings.add(bind);
                        }
                    }
                }
            }
        }

        public List<di_Binding> get() {
            List<di_Binding> matchedBindings = di_PlatformCache.getInstance().retrieveBindings(this.developerName, this.bindingObject);
            if ((di_PlatformCache.isStoringBindingInPlatformCache() && bindingsAreRequired && matchedBindings.isEmpty()) || 
                !di_PlatformCache.isStoringBindingInPlatformCache()) {
                loadBindings();
                for (di_Binding bind : bindings) {
                    if (isBindingMatchByFilteringCriteria(bind)) {
                        matchedBindings.add(bind);
                    }
                }
            }
            this.developerName = null;
            this.bindingObject = null;
            matchedBindings.sort();
            this.bindingsAreRequired = true;
            return matchedBindings;
        }
    }

    public class BindingException extends Exception {}

    public enum BindingType { Apex, VisualforceComponent, LightningComponent, Flow, Module }

    public static di_Binding newInstance(
            BindingType bindType,
            String developerName,
            SObjectType bindingObject,
            Integer bindingSequence,
            Object to,
            Object bindingData) {
        Type implType = BINDING_IMPLS_BY_TYPE.get(bindType);
        if (implType != null) {
            di_Binding binding = (di_Binding) implType.newInstance();
            binding.BindingType = bindType;
            binding.DeveloperName = developerName;
            binding.BindingObject = bindingObject;
            binding.BindingSequence = bindingSequence;
            binding.To = to;
            binding.Data = bindingData;
            return binding;
        }
        throw new BindingException('Binding type ' + bindType + ' has no implementation.');
    }

    private static final Map<BindingType, Type> BINDING_IMPLS_BY_TYPE = new Map<BindingType, Type> {
        BindingType.Apex => ApexBinding.class,
        BindingType.LightningComponent => LightningComponentBinding.class,
        BindingType.VisualforceComponent => VisualForceComponentBinding.class,
        BindingType.Flow => FlowBinding.class,
        BindingType.Module => ApexBinding.class
    };

    private class ApexBinding extends di_Binding {
        public override Object newInstance(Object params) {
            if (To instanceof String) {
                String className = (String) To;
                Type toType = NamespacePrefix == null ? Type.forName(className) : Type.forName(NamespacePrefix, className);
                if (toType == null) {
                    throw new BindingException('Apex binding ' + DeveloperName + ' implementation ' + To + ' does not exist');
                }
                Object toObject = toType.newInstance();
                IsProvider = toObject instanceof Provider;
                if (IsProvider) {
                    return ((Provider) toObject).newInstance(params);
                } else if (params != null) {
                    throw new BindingException('Apex binding ' + DeveloperName + ' implementation ' + className + ' does not implement the Provider interface.');
                }
                return toObject;
            }
            return To;
        }
    }

    private class VisualForceComponentBinding extends di_Binding {
        public override Object newInstance(Object params) {
            if (To instanceof String) {
                String className = (String) To;
                Type toType = NamespacePrefix == null ? Type.forName(className) : Type.forName(NamespacePrefix, className);
                if (toType == null) {
                    throw new BindingException('Visualforce Component binding ' + DeveloperName + ' implementation ' + className + ' does not exist.');
                }
                Object toObject = toType.newInstance();
                IsProvider = toObject instanceof Provider;
                if (IsProvider) {
                    return ((Provider) toObject).newInstance(params);
                }
                throw new BindingException('Visualforce Component binding ' + DeveloperName + ' must point to a class implementing the Provider interface.');
            }
            return To;
        }
    }

    private class LightningComponentBinding extends di_Binding {
        public override Object newInstance(Object params) {
            return To;
        }
    }

    private class FlowBinding extends di_Binding {
        public override Object newInstance(Object params) {
            if (To instanceof String) {
                String flowName = (String) To;
                if (params instanceof Map<String, Object>) {
                    return new di_Flow(Flow.Interview.createInterview(flowName, (Map<String, Object>) params));
                }
                return new di_Flow(Flow.Interview.createInterview(flowName, new Map<String, Object>()));
            }
            return To;
        }
    }
}
```

File `classes\di_Injector.cls`:
```apex
public class di_Injector {
    @testVisible
    private static List<di_BindingConfigWrapper> mock_BindingConfigurationWrappersOuter = null;
    
    public static final di_Injector Org =
        new di_Injector(
            new List<di_Module> {
        new CustomMetadataModule()
    });

    @TestVisible
    public di_Binding.Resolver Bindings {get; private set;}

    public di_Injector(di_Module module) {
        this(new List<di_Module> { module });
    }

    public di_Injector(List<di_Module> modules) {
        Bindings = new di_Binding.Resolver(modules);
    }

    public Object getInstance(Type developerNameByType) {
        return getInstance(developerNameByType?.getName()?.toLowerCase(), null);
    }

    public Object getInstance(Type developerNameByType, Object params) {
        return getInstance(developerNameByType?.getName()?.toLowerCase(), params);
    }

    public Object getInstance(String developerName) {
        return getInstance(developerName, null);
    }

    public Object getInstance(String developerName, Object params) {
        if (String.isBlank(developerName)) {
            throw new InjectorException('Request for Binding cannot take "developerName" parameter of null');
        }
        List<di_Binding> bindingsFound = this.Bindings.byName(developerName.toLowerCase().trim()).get();
        if (bindingsFound.isEmpty()) {
            throw new InjectorException('Binding for "' + developerName + '" not found');
        }
        return bindingsFound[0].getInstance(params);
    }

    public Object getInstance(String developerName, Schema.SObjectType bindingSObjectType, Object params) {
        if (String.isBlank(developerName)) {
            throw new InjectorException('Request for Binding cannot take "developerName" parameter of null');
        }
        if (bindingSObjectType == null) {
            throw new InjectorException('Request for Binding cannot take "bindingSObjectType" parameter of null');
        }
        List<di_Binding> bindingsFound = this.Bindings.bySObject(bindingSObjectType)
                                         .byName(developerName.toLowerCase().trim())
                                         .get();
        if (bindingsFound.isEmpty()) {
            throw new InjectorException('Binding for "' + developerName + '" and SObjectType "' + bindingSObjectType + '" not found');
        }
        return bindingsFound[0].getInstance(params);
    }

    @testVisible
    private class CustomMetadataModule extends di_Module {
        private string bindingObjectApiName = null;
        
        @testVisible
        private List<di_BindingConfigWrapper> getDIBinding(){
            List<di_BindingConfigWrapper> recordList = new List<di_BindingConfigWrapper>();
            if(di_Injector.mock_BindingConfigurationWrappersOuter != null) {
                recordList = di_Injector.mock_BindingConfigurationWrappersOuter;
            } else {
                List<di_Binding__mdt> bindingMDTRec = [SELECT QualifiedAPIName
                                                            , DeveloperName
                                                            , BindingName__c
                                                            , NamespacePrefix
                                                            , Type__c
                                                            , To__c
                                                            , BindingObject__c
                                                            , BindingObject__r.QualifiedApiName
                                                            , BindingObjectAlternate__c
                                                            , BindingSequence__c
                                                         FROM di_Binding__mdt];
                for(di_Binding__mdt records :bindingMDTRec) {
                    recordList.add(new di_BindingConfigWrapper(records));
                }
            }
            return recordList;
        }
        public override void configure() {
            for(di_BindingConfigWrapper bindingConfig :getDIBinding()) {
                bind(bindingConfig.DeveloperName);
                type(bindingConfig.Type);
                if(String.isNotBlank(bindingConfig.BindingObject) || String.isNotBlank(bindingConfig.BindingObjectAlternate)) {
                    bindingObjectApiName = String.isNotBlank(bindingConfig.BindingObject) 
                                            ? bindingConfig.bindingObjectQualifiedApiName.toLowerCase().trim() 
                                            : bindingConfig.BindingObjectAlternate.toLowerCase().trim();
                    Schema.DescribeSobjectResult[] results = Schema.describeSObjects(new String[] { bindingObjectApiName });
                    if(results.size() != 1) {
                        throw new InjectorException('Failed to find SObject ' + bindingObjectApiName + ' referenced by binding ' + bindingConfig.DeveloperName);
                    }
                    bind(results[0].getSObjectType()); 
                }
                if(bindingConfig.BindingSequence != null) {
                    sequence(Integer.valueOf(bindingConfig.BindingSequence));
                }
                data(bindingConfig);
                to(bindingConfig.To);
            }
        }
    }

    public class InjectorException extends Exception {}
}
```