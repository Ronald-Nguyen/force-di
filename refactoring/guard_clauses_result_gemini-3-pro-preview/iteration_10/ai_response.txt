File `classes/di_BindingParam.cls`:
```apex
public with sharing class di_BindingParam {

    public static Map<String, Object> parameters { get; set; }

    @InvocableMethod(
        label = 'Get Value'
    )
    public static List<Response> invoke( List<Request> requests ) {

        List<Response> responses = new List<Response>();

        for ( Request req : requests ) {

            Response res = new Response();

            if ( parameters == null ) {
                System.debug( LoggingLevel.WARN, 'BindingParam.parameters is null' );
                System.debug( 'paramName=' + req );
                System.debug( res );
                responses.add( res );
                continue;
            }

            Object value = parameters.get( req.paramName );

            if ( value == null ) {
                System.debug( LoggingLevel.WARN, 'BindingParam.parameters.get("' + req + '") is null' );
                System.debug( 'paramName=' + req );
                System.debug( res );
                responses.add( res );
                continue;
            }

            processValue(res, value);
            synchronizeSinglesAndCollections(res);

            System.debug( 'paramName=' + req );
            System.debug( res );

            responses.add( res );
        }

        return responses;
    }

    private static void processValue(Response res, Object value) {
        if ( value instanceof Decimal ) {
            handleDecimal(res, (Decimal) value);
        } else if ( value instanceof Decimal[] ) {
            handleDecimalArray(res, (Decimal[]) value);
        } else if ( value instanceof String ) {
            handleString(res, (String) value);
        } else if ( value instanceof String[] ) {
            handleStringArray(res, (String[]) value);
        } else if ( value instanceof Boolean ) {
            handleBoolean(res, (Boolean) value);
        } else if ( value instanceof Boolean[] ) {
            handleBooleanArray(res, (Boolean[]) value);
        } else if ( value instanceof Date ) {
            handleDate(res, (Date) value);
        } else if ( value instanceof Date[] ) {
            handleDateArray(res, (Date[]) value);
        } else if ( value instanceof DateTime ) {
            handleDateTime(res, (DateTime) value);
        } else if ( value instanceof DateTime[] ) {
            handleDateTimeArray(res, (DateTime[]) value);
        }
    }

    private static void handleDecimal(Response res, Decimal value) {
        res.longValue = toLongValue( value );
        res.decimalValue = value;
        res.stringValue = toStringValue( value );
        res.booleanValue = toBooleanValue( value );
        res.dateValue = toDateValue( value );
        res.dateTimeValue = toDateTimeValue( value );
    }

    private static void handleDecimalArray(Response res, Decimal[] values) {
        res.decimalValues = values;
        for ( Decimal val : values ) {
            res.longValues.add( toLongValue( val ) );
            res.stringValues.add( toStringValue( val ) );
            res.booleanValues.add( toBooleanValue( val ) );
            res.dateValues.add( toDateValue( val ) );
            res.dateTimeValues.add( toDateTimeValue( val ) );
        }
    }

    private static void handleString(Response res, String value) {
        res.longValue = toLongValue( value );
        res.decimalValue = toDecimalValue( value );
        res.stringValue = value;
        res.booleanValue = toBooleanValue( value );
        res.dateValue = toDateValue( value );
        res.dateTimeValue = toDateTimeValue( value );
    }

    private static void handleStringArray(Response res, String[] values) {
        res.stringValues = values;
        for ( String val : values ) {
            res.longValues.add( toLongValue( val ) );
            res.decimalValues.add( toDecimalValue( val ) );
            res.booleanValues.add( toBooleanValue( val ) );
            res.dateValues.add( toDateValue( val ) );
            res.dateTimeValues.add( toDateTimeValue( val ) );
        }
    }

    private static void handleBoolean(Response res, Boolean value) {
        res.longValue = toLongValue( value );
        res.decimalValue = toDecimalValue( value );
        res.stringValue = toStringValue( value );
        res.booleanValue = value;
        res.dateValue = toDateValue( value );
        res.dateTimeValue = toDateTimeValue( value );
    }

    private static void handleBooleanArray(Response res, Boolean[] values) {
        res.booleanValues = values;
        for ( Boolean val : values ) {
            res.longValues.add( toLongValue( val ) );
            res.decimalValues.add( toDecimalValue( val ) );
            res.stringValues.add( toStringValue( val ) );
            res.dateValues.add( toDateValue( val ) );
            res.dateTimeValues.add( toDateTimeValue( val ) );
        }
    }

    private static void handleDate(Response res, Date value) {
        res.longValue = toLongValue( value );
        res.decimalValue = toDecimalValue( value );
        res.stringValue = toStringValue( value );
        res.booleanValue = toBooleanValue( value );
        res.dateValue = value;
        res.dateTimeValue = toDateTimeValue( value );
    }

    private static void handleDateArray(Response res, Date[] values) {
        res.dateValues = values;
        for ( Date val : values ) {
            res.longValues.add( toLongValue( val ) );
            res.decimalValues.add( toDecimalValue( val ) );
            res.stringValues.add( toStringValue( val ) );
            res.booleanValues.add( toBooleanValue( val ) );
            res.dateTimeValues.add( toDateTimeValue( val ) );
        }
    }

    private static void handleDateTime(Response res, DateTime value) {
        res.longValue = toLongValue( value );
        res.decimalValue = toDecimalValue( value );
        res.stringValue = toStringValue( value );
        res.booleanValue = toBooleanValue( value );
        res.dateValue = toDateValue( value );
        res.dateTimeValue = value;
    }

    private static void handleDateTimeArray(Response res, DateTime[] values) {
        res.dateTimeValues = values;
        for ( DateTime val : values ) {
            res.longValues.add( toLongValue( val ) );
            res.decimalValues.add( toDecimalValue( val ) );
            res.stringValues.add( toStringValue( val ) );
            res.booleanValues.add( toBooleanValue( val ) );
            res.dateValues.add( toDateValue( val ) );
        }
    }

    private static void synchronizeSinglesAndCollections(Response res) {
        if ( res.longValue     == null && !res.longValues.isEmpty() )     { res.longValue = res.longValues[0]; }
        if ( res.decimalValue  == null && !res.decimalValues.isEmpty() )  { res.decimalValue = res.decimalValues[0]; }
        if ( res.stringValue   == null && !res.stringValues.isEmpty() )   { res.stringValue = res.stringValues[0]; }
        if ( res.booleanValue  == null && !res.booleanValues.isEmpty() )  { res.booleanValue = res.booleanValues[0]; }
        if ( res.dateValue     == null && !res.dateValues.isEmpty() )     { res.dateValue = res.dateValues[0]; }
        if ( res.dateTimeValue == null && !res.dateTimeValues.isEmpty() ) { res.dateTimeValue = res.dateTimeValues[0]; }

        if ( res.longValue     != null && res.longValues.isEmpty() )     { res.longValues.add( res.longValue ); }
        if ( res.decimalValue  != null && res.decimalValues.isEmpty() )  { res.decimalValues.add( res.decimalValue ); }
        if ( res.stringValue   != null && res.stringValues.isEmpty() )   { res.stringValues.add( res.stringValue ); }
        if ( res.booleanValue  != null && res.booleanValues.isEmpty() )  { res.booleanValues.add( res.booleanValue ); }
        if ( res.dateValue     != null && res.dateValues.isEmpty() )     { res.dateValues.add( res.dateValue ); }
        if ( res.dateTimeValue != null && res.dateTimeValues.isEmpty() ) { res.dateTimeValues.add( res.dateTimeValue ); }
    }

    private static Long toLongValue( Decimal value ) {
        return ( value == null ? null : value.longValue() );
    }

    private static Long toLongValue( String value ) {
        return ( ( value == null || !value.isNumeric() ) ? null : Long.valueOf( value ) );
    }

    private static Long toLongValue( Boolean value ) {
        return ( value == null ? null : value ? 1 : 0 );
    }

    private static Long toLongValue( Date value ) {
        return ( value == null ? null : toLongValue(
            DateTime.newInstance( value.year(), value.month(), value.day(), 0, 0, 0 )
        ));
    }

    private static Long toLongValue( DateTime value ) {
        return ( value == null ? null : value.getTime() );
    }

    private static Decimal toDecimalValue( String value ) {
        return ( ( value == null || !value.isNumeric() ) ? null : Decimal.valueOf( value ) );
    }

    private static Decimal toDecimalValue( Boolean value ) {
        return ( value == null ? null : value ? 1 : 0 );
    }

    private static Decimal toDecimalValue( Date value ) {
        return ( value == null ? null : toDecimalValue(
            DateTime.newInstance( value.year(), value.month(), value.day(), 0, 0, 0 )
        ));
    }

    private static Decimal toDecimalValue( DateTime value ) {
        return ( value == null ? null : value.getTime() );
    }

    private static String toStringValue( Date value ) {
        return ( value == null ? null : String.valueOf( value ) );
    }

    private static String toStringValue( DateTime value ) {
        return ( value == null ? null : String.valueOf( value ) );
    }

    private static String toStringValue( Object value ) {
        return ( value == null ? null : String.valueOf( value ) );
    }

    private static Boolean toBooleanValue( Decimal value ) {
        return ( value == null ? false : ( value > 0 ) );
    }

    private static Boolean toBooleanValue( String value ) {
        return ( value == null ? false : Boolean.valueOf( value ) );
    }

    private static Boolean toBooleanValue( Date value ) {
        return ( value == null ? false : true );
    }

    private static Boolean toBooleanValue( DateTime value ) {
        return ( value == null ? false : true );
    }

    private static Date toDateValue( Decimal value ) {
        return ( value == null ? null : DateTime.newInstance( value.longValue() ).date() );
    }

    private static Date toDateValue( String value ) {
        try {
            return ( value == null ? null : Date.valueOf( value ) ); 
        } catch ( Exception e ) {
            return null;
        }
    }

    private static Date toDateValue( Boolean value ) {
        return null;
    }

    private static Date toDateValue( DateTime value ) {
        return ( value == null ? null : value.date() );
    }

    private static DateTime toDateTimeValue( Decimal value ) {
        return ( value == null ? null : DateTime.newInstance( value.longValue() ) );
    }

    private static DateTime toDateTimeValue( String value ) {
        try {
            return ( value == null ? null : DateTime.valueOf( value ) ); 
        } catch ( Exception e ) {
            return toDateValue( value ); 
        }
    }

    private static DateTime toDateTimeValue( Boolean value ) {
        return null;
    }

    private static DateTime toDateTimeValue( Date value ) {
        return ( value == null ? null : DateTime.newInstance( value.year(), value.month(), value.day(), 0, 0, 0 ) );
    }

    public class Request {

        @InvocableVariable(
            label = 'Parameter Name'
            required = true
        )
        public String paramName;

    }

    public class Response {

        @InvocableVariable(
            label = 'Number'
            description = 'Whole number (no decimals)'
        )
        public Long longValue;

        @InvocableVariable(
            label = 'Decimal'
            description = 'Number that may include decimal places'
        )
        public Decimal decimalValue;

        @InvocableVariable(
            label = 'Text'
            description = 'Text value'
        )
        public String stringValue;

        @InvocableVariable(
            label = 'Boolean'
            description = 'true/false'
        )
        public Boolean booleanValue;

        @InvocableVariable(
            label = 'Date'
        )
        public Date dateValue;

        @InvocableVariable(
            label = 'Date/Time'
        )
        public DateTime dateTimeValue;

        @InvocableVariable(
            label = 'Number Collection'
            description = 'Collection of whole numbers'
        )
        public Long[] longValues = new Long[] {};

        @InvocableVariable(
            label = 'Decimal Collection'
            description = 'Collection of numbers that may include decimal places'
        )
        public Decimal[] decimalValues = new Decimal[] {};

        @InvocableVariable(
            label = 'Text Collection'
            description = 'Collection of text values'
        )
        public String[] stringValues = new String[] {};

        @InvocableVariable(
            label = 'Boolean Collection'
            description = 'Collection of true/false values'
        )
        public Boolean[] booleanValues = new Boolean[] {};

        @InvocableVariable(
            label = 'Date Collection'
            description = 'Collection of date values'
        )
        public Date[] dateValues = new Date[] {};

        @InvocableVariable(
            label = 'Date/Time Collection'
            description = 'Collection of date/time values'
        )
        public DateTime[] dateTimeValues = new DateTime[] {};

    }

}
```

File `classes/di_PlatformCache.cls`:
```apex
public with sharing class di_PlatformCache
{
    public static final Integer NUM_OF_SECS_IN24HR =  86400;

    @TestVisible
    private static final String BINDING_KEY = 'bindingKeyIndex';
    @TestVisible
    private static di_PlatformCache instance;
    @TestVisible
    private Map<String, Map<Schema.SObjectType, Set<String> > > cacheKeyIndexMap = new Map<String, Map<Schema.SObjectType, Set<String> > >();
    @TestVisible
    private static Map<String, String> generatedKeyNames = new Map<String, String>();
    
    @TestVisible
    private static Boolean DEBUGGING = false;

    private di_PlatformCache() 
    {
    }

    public static di_PlatformCache getInstance()
    {
        if ( instance == null )
        {
            instance = new di_PlatformCache();
        }

        return instance;
    }


    public static Boolean isStoringBindingInPlatformCache()
    {
        return getConfig().UsePlatformCacheToStoreBindings__c == null ? false : ( getConfig().UsePlatformCacheToStoreBindings__c && getPartition() != null );
    }

    public Map<String, Map<Schema.SObjectType, Set<String>>> getCacheKeyIndexMap()
    {
        if ( cacheKeyIndexMap.isEmpty() )
        {
            try 
            {
                Cache.OrgPartition orgPartition = getPartition();
                if ( orgPartition != null ) 
                {
                    cacheKeyIndexMap = (Map<String, Map<Schema.SObjectType, Set<String> > >) orgPartition.get( getKeyIndexName() );
                }
            }
            catch (Cache.Org.OrgCacheException ex) 
            {
                cacheKeyIndexMap = null;
            }

            if ( cacheKeyIndexMap == null )
            {
                cacheKeyIndexMap = new Map<String, Map<Schema.SObjectType, Set<String> > >();
            }
        }

        return cacheKeyIndexMap;
    }

    public Boolean addBindingToPlatformCache( di_Binding binding )
    {
        if ( !isStoringBindingInPlatformCache() ) {
            return false;
        }
            
        Cache.OrgPartition orgPartition = getPartition();
        if ( orgPartition == null ) {
            return false;
        }

        String theKeyName = getKeyName(binding);
        
        List<di_Binding> workingBindings = new List<di_Binding>();

        if ( orgPartition.contains(theKeyName) )
        {
            workingBindings.addAll((List<di_Binding>) orgPartition.get(theKeyName));
        }
        
        workingBindings.add( binding );
        log('Adding binding for hash => ' + theKeyName + ' && developerName => ' + binding.developerName + ' && object => ' + binding.bindingObject);
        
        try 
        {
            orgPartition.put(theKeyName, workingBindings, getPartitionTTL(), Cache.Visibility.ALL, false);    
        }
        catch (cache.Org.OrgCacheException oce)
        {
            log( oce );
        }

        addBindingToKeyIndex(binding);
        
        return true;
    }

    public list<di_Binding> retrieveBindings(String developerName, Schema.SObjectType bindingSObjectType)
    {
        list<di_Binding> bindings = new list<di_Binding>();

        if ( !isStoringBindingInPlatformCache() || String.isBlank(developerName) || bindingSObjectType == null )
        {
            return bindings;
        }

        log('Retrieving from Cache Key => ' + developerName + ' && Cache Key Index => ' + bindingSObjectType);
        
        Cache.OrgPartition orgPartition = getPartition();
        if (orgPartition == null) {
            return bindings;
        }

        Map<Schema.SObjectType, Set<String> > keyIndexBySObjectTypeMap = getCacheKeyIndexMap().get(developerName.toLowerCase().trim());
        if ( keyIndexBySObjectTypeMap == null )
        {
            return bindings;
        }

        Set<String> cacheKeys = keyIndexBySObjectTypeMap.get(bindingSObjectType);
        if ( cacheKeys == null )
        {
            return bindings;
        }
        
        Object cachedObject = null;
        for ( String cacheKey : cacheKeys )
        {
            cachedObject = orgPartition.get( cacheKey );
            if ( cachedObject != null )
            {
                bindings.addAll( (List<di_Binding>) cachedObject);
            }
        }

        return bindings;
    }

    public static void clearCachedBindings() {

        Set<String> keys = getPartitionKeys();

        if ( keys != null ) 
        {
            String partitionKey = getPartitionName();
            Cache.OrgPartition partition = getPartition();
            
            for (String key : partition.getKeys() ) 
            {
                try 
                {
                    partition.remove(key);
                } 
                catch (Exception ex) 
                {
                    log('XX]> Unable to remove Platform Cache partition [' + partitionKey + '] key [' + key + ']');
                }
            }
        }
    }

    
    public static Set<String> getPartitionKeys() 
    {
        Set<String> keys = null;
        Cache.OrgPartition partition = getPartition();
        if ( partition != null ) 
        {
            keys = partition.getKeys();
        }
        return keys;
    }

    @TestVisible
    private static di_Configurations__c getConfig()
    {
        di_Configurations__c config = di_Configurations__c.getInstance();

        if ( config == null )
        {
            config = new di_Configurations__c();
            config.SetupOwnerId = ConnectApi.Organization.getSettings().orgId;
            
            if (di_Configurations__c.SObjectType.getDescribe().isCreateable() )
            {
                insert config;
            }
        }

        return config;
    }

    @TestVisible
    private static String getPartitionName()
    {
        return getConfig().OrgCachePartitionName__c;
    }

    @TestVisible
    private Integer getPartitionTTL()
    {
        return NUM_OF_SECS_IN24HR; 
    }

    @TestVisible
    private static Cache.OrgPartition getPartition()
    {
        Cache.OrgPartition result = null;
        try 
        {
            result = Cache.Org.getPartition( getPartitionName() );
        }
        catch (Exception excp) 
        {
            log('ERROR: Is there Cache? Is the Cache Partition enabled : Exception:' + excp);
        }
        return result;
    }

    @TestVisible
    private boolean pushCacheKeyIndexMapToCache()
    {
        Cache.OrgPartition orgPartition = getPartition();
        if ( orgPartition != null ) 
        {
            orgPartition.put( getKeyIndexName(), this.cacheKeyIndexMap, getPartitionTTL(), Cache.Visibility.ALL, false);
        }
        return orgPartition != null;
    }

    @TestVisible
    private void addBindingToKeyIndex(di_Binding binding)
    {
        if (binding == null) {
            return;
        }
        String workingDeveloperName = binding.developerName.toLowerCase().trim();

        if ( !getCacheKeyIndexMap().containsKey( workingDeveloperName ) )
        {
            getCacheKeyIndexMap().put(workingDeveloperName, new Map<Schema.SObjectType, Set<String> >() );
        }

        if ( !getCacheKeyIndexMap().get(workingDeveloperName).containsKey( binding.bindingObject) )
        {
            getCacheKeyIndexMap().get(workingDeveloperName).put( binding.bindingObject, new Set<String>() );
        }

        getCacheKeyIndexMap().get(workingDeveloperName).get(binding.bindingObject).add(getKeyName(binding));

        pushCacheKeyIndexMapToCache();
    }

    @TestVisible
    private String constructKeyName( Schema.SObjectType bindingSObjectType, String developerName )
    {
        if (String.isBlank(developerName)) {
            return null;
        }
        String objectNamePart = ( bindingSObjectType != null ) ? bindingSObjectType.getDescribe().getName().toLowerCase().replaceAll('__','') : '';
        String devNamePart = String.isBlank(developerName) ? '' : developerName.toLowerCase().trim();
        String key = objectNamePart + devNamePart;

        if (generatedKeyNames.containsKey(key)) 
        { 
            return generatedKeyNames.get(key); 
        }

        String hash = String.valueOf( Math.abs( ( key ).hashcode() ) );
        log('Creating Hash For => ' + developerName + ' && ' + bindingSObjectType + ' := ' + hash);
        generatedKeyNames.put(key, hash);
        return hash;
    }

    @TestVisible
    private String getKeyName( String developerName, Schema.SObjectType bindingSObjectType)
    {
        return constructKeyName( bindingSObjectType, developerName);
    }
    
    @TestVisible
    private String getKeyName( di_Binding binding )
    {
        return constructKeyName( binding.bindingObject, binding.developerName);
    }

    @TestVisible
    private String getKeyIndexName()
    {
        return di_PlatformCache.BINDING_KEY;
    }

    
    @TestVisible
    private static Boolean log(Exception excp)
    {
        Boolean before = DEBUGGING, result;
        
        DEBUGGING = true;
        result = log('Exception: ' + excp.getMessage());
        DEBUGGING = before;
        return result;

    }

    
    @TestVisible
    private static Boolean log(String message)
    {
        Boolean doLog = DEBUGGING && string.isNotBlank(message);
        if ( doLog ) 
        {
            system.debug('+++ di_PlatformCache.message: ' + message);
        }
        return doLog;
    }
}
```